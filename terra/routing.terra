#include "/home/terra/TerraNet_v0.1/terra/TerraNet.defs"

pktype usrMsg from radioMsg with
	var ubyte d8;
	var ushort d16;
end
var usrMsg msgRadio;
msgRadio.d8 = 0;

var ushort nodeId    = getNodeId();
var ushort fatherId  = 1;
var ubyte  nodeDepth = 0;

// Builds Tree
if nodeId != 11 then
	msgRadio = await RECEIVE(0);

	fatherId = msgRadio.source;
	nodeDepth = msgRadio.d8;
end

msgRadio.type   = 0;
msgRadio.source = nodeId;
msgRadio.target = BROADCAST;
msgRadio.d8     = nodeDepth + 1;
emit SEND(msgRadio);
await SEND_DONE;

var ubyte stat = qClear();
msgRadio.d8    = 0;

// Exchange Data
par do
	var usrMsg msgRadio;
	loop do
		emit REQ_TEMP();
		var ushort temp = await TEMP;
		msgRadio.source = nodeId;
		msgRadio.type   = 1;
		msgRadio.d16    = temp;
		inc msgRadio.d8;
		stat = qPut(msgRadio);
		await 60s;
	end
with
	var usrMsg msgRadio;
	loop do
		msgRadio = await RECEIVE(1);
		stat = qPut(msgRadio);
	end
with
	var usrMsg msgRadio;
	var ubyte ack;
	loop do
		await Q_READY;
		loop do
			if (qSize() == 0) then
				break;
			end
			stat = qGet(msgRadio);

			msgRadio.target = fatherId;

			loop do
				emit SEND_ACK(msgRadio);
				ack = await SEND_DONE_ACK;
				if (ack) then
					emit LED1(ON);
					break;
				else
					emit LED0(ON);
					await (10 * (random() % 100))ms;
				end
			end
			await (500 * nodeDepth)ms;
		end
	end	
end
